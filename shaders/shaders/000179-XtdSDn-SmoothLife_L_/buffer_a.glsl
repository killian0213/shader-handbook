// Buf A (buffer) — SmoothLife(L)  by chronos
// https://www.shadertoy.com/view/XtdSDn


//Conventions:
// x component = outer radius / ring
// y component = inner radius / disk
/*
   _
 /   \
|  O  |
 \ _ /
*/
const float PI = 3.14159265;
const float dt = 0.30;


const vec2 r = vec2(10.0, 3.0);

// SmoothLifeL rules
const float b1 = 0.257;
const float b2 = 0.336;
const float d1 = 0.365;
const float d2 = 0.549;

const float alpha_n = 0.028;
const float alpha_m = 0.147;
/*------------------------------*/

//const float KEY_LEFT  = 37.5/256.0;
const float KEY_UP    = 38.5/256.0;
//const float KEY_RIGHT = 39.5/256.0;
const float KEY_DOWN  = 40.5/256.0;
const float KEY_SPACE  = 32.5/256.0;


// 1 out, 3 in... <https://www.shadertoy.com/view/4djSRW>
#define MOD3 vec3(.1031,.11369,.13787)
float hash13(vec3 p3) {
	p3 = fract(p3 * MOD3);
    p3 += dot(p3, p3.yzx+19.19);
    return fract((p3.x + p3.y)*p3.z);
}


/* ---------------- Sigmoid functions ------------------------------------ */

// TODO: reduce unnecessary parameters (remove arguments, use global consts)

float sigmoid_a(float x, float a, float b) {
    return 1.0 / (1.0 + exp(-(x - a) * 4.0 / b));
}

// unnecessary 
float sigmoid_b(float x, float b, float eb) {
    return 1.0 - sigmoid_a(x, b, eb);
}

float sigmoid_ab(float x, float a, float b, float ea, float eb) {
    return sigmoid_a(x, a, ea) * sigmoid_b(x, b, eb);
}

float sigmoid_mix(float x, float y, float m, float em) {
    return x * (1.0 - sigmoid_a(m, 0.5, em)) + y * sigmoid_a(m, 0.5, em);
}

/* ----------------------------------------------------------------------- */

// SmoothLifeL
float transition_function(vec2 disk_ring) {
    return sigmoid_mix(sigmoid_ab(disk_ring.x, b1, b2, alpha_n, alpha_n),
                       sigmoid_ab(disk_ring.x, d1, d2, alpha_n, alpha_n), disk_ring.y, alpha_m
                      );
}

// unnecessary (?)
float ramp_step(float steppos, float t) {
    return clamp(t-steppos+0.5, 0.0, 1.0);
}

// unnecessary
vec2 wrap(vec2 position) { return fract(position); }

// Computes both inner and outer integrals
// TODO: Optimize. Much redundant computation. Most expensive part of program.
vec2 convolve(vec2 uv) {
    vec2 result = vec2(0.0);
    for (float dx = -r.x; dx <= r.x; dx++) {
        for (float dy = -r.x; dy <= r.x; dy++) {
            vec2 d = vec2(dx, dy);
            float dist = length(d);
            vec2 offset = d / iResolution.xy;
            vec2 samplepos = wrap(uv + offset);
            //if(dist <= r.y + 1.0) {
                float weight = texture(iChannel0, samplepos).x;
            	result.x += weight * ramp_step(r.y, dist) * (1.0-ramp_step(r.x, dist));	
            	
            //} else if(dist <= r.x + 1.) {
                //float weight = texture(iChannel0, uv+offset).x;
				result.y += weight * (1.0-ramp_step(r.y, dist));
            //}
        }
    }
    return result;
}





void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec3 color = vec3(0.0);
    
    vec2 uv = fragCoord.xy / iResolution.xy;
    
    // Compute inner disk and outer ring area.
    vec2 area = PI * r * r;
    area.x -= area.y;
    /* -------------------------------------*/
    
    // TODO: Cleanup.
    color = texture(iChannel0, uv).xyz;
    vec2 normalized_convolution = convolve(uv.xy).xy / area;
    color.x = color.x + dt * (2.0 * transition_function(normalized_convolution) - 1.0);
    color.yz = normalized_convolution;
    color = clamp(color, 0.0, 1.0);
    
    // Set initial conditions. TODO: Move to function / cleanup
    if(iFrame < 10 || texture( iChannel2, vec2(KEY_SPACE,0.5) ).x > 0.5) {
        color = vec3(hash13(vec3(fragCoord, iFrame)) - texture(iChannel1, uv).x + 0.5);
    }
    
    if(iMouse.z > 0.) {
        //vec2 dst = abs(uv - iMouse.xy/iResolution.xy);
        float dst = length((fragCoord.xy - iMouse.xy)/iResolution.xx);
        /*if(max(dst.x * iResolution.x/iResolution.y, dst.y) < 0.05) {
        	color = vec3(hash13(vec3(fragCoord, iFrame)) - texture(iChannel1, uv).x + 0.5);
        }*/
        if(dst <= (r.x)/iResolution.x) {
        	color.x = step((r.y+1.5)/iResolution.x, dst) * (1.0 - step(r.x/iResolution.x, dst));
        }
        /*if(dst <= (r.x)/iResolution.x) {
        	color.x = step((r.y+1.0)/iResolution.x, dst) * (1.0 - step((r.x-0.5)/iResolution.x, dst));
        }*/
    }
    
    // Inspect transition function. TODO: Move to function / ifdef
    if(texture( iChannel2, vec2(KEY_DOWN, 5.0/3.0) ).x > 0.5) {
        color = vec3(transition_function(uv));
    }
    
    if(texture( iChannel2, vec2(KEY_UP, 0.5)).x > 0.5) {
    	color = vec3(0.0);
    }
    
    fragColor = vec4(color, 1.0);
}