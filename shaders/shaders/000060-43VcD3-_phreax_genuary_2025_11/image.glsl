// Image (image) — [phreax] genuary 2025 #11 by phreax
// https://www.shadertoy.com/view/43VcD3

/* Creative Commons Licence Attribution-NonCommercial-ShareAlike 
   phreax/jiagual 2025
   
   Inspired by tdhoopers work: https://www.shadertoy.com/view/WdB3Dw

*/

#define PI 3.141592
#define TAU (2.*PI)
#define SIN(x) (sin(x)*.5+.5)
#define BUMP_EPS 0.004
#define sabsk(x, k) sqrt(x * x + k * k)
#define sabs(x) (sabsk(x, .1))
#define S(a, b, x) smoothstep(a, b, x)


float tt, g_mat;
vec3 ro;

mat2 rot(float a) { return mat2(cos(a), -sin(a), sin(a), cos(a)); }



float smin(float a, float b, float k) {
  float h = clamp((a-b)/k * .5 + .5, 0.0, 1.0);
  return mix(a, b, h) - h*(1.-h)*k;
}



float pMod(float p, float size) {
	float halfsize = size*0.5;
	float c = floor((p + halfsize)/size);
	p = mod(p + halfsize, size) - halfsize;
	return p;
}


float n21(vec2 p) {
      return fract(sin(dot(p, vec2(524.423, 123.34)))*3228324.345);
}

// smooth noise
float noise(vec2 n) {
    const vec2 d = vec2(0., 1.0);
    vec2 b = floor(n);
    vec2 f = mix(vec2(0.0), vec2(1.0), fract(n));
    return mix(mix(n21(b), n21(b + d.yx), f.x), mix(n21(b + d.xy), n21(b + d.yy), f.x), f.y);
}


vec3 g_p;
float smax( float a, float b, float k )
{
    float h = max(k-abs(a-b),0.0);
    return max(a, b) + h*h*0.25/k;
}

vec3 fold(vec3 p) {

    float c = cos(PI/5.), s = sqrt(.75 - c*c);
    
    vec3 n = vec3(-.5, -c, s);
    
    p = abs(p);;
    p -= 2.*min(0., dot(p, n))*n;
    
    p.xy = abs(p.xy);
    p -= 2.*min(0., dot(p, n))*n;
    
    p.xy = abs(p.xy);
    p -= 2.*min(0., dot(p, n))*n;
    
    return p;
}


// from https://www.shadertoy.com/view/WdB3Dw
vec4 inverseStereographic(vec3 p, out float k) {
    k = 2.0/(1.0+dot(p,p));
    return vec4(k*p,k-1.0);
}

float fTorus(vec4 p4) {
    float d1 = length(p4.xy) / length(p4.zw) - 1.;
    float d2 = length(p4.zw) / length(p4.xy) - 1.;
    float d = d1 < 0. ? -d1 : d2;
    d /= PI;
    return d;
}

float fixDistance(float d, float k) {
    float sn = sign(d);
    d = abs(d);
    d = d / k * 1.82;
    d += 1.;
    d = pow(d, .5);
    d -= 1.;
    d *= 5./3.;
    d *= sn;
    return d;
}

float map(vec3 p) {  
    // if is symmetric p = abs(p); 
    p.xy *= rot(.1*tt);
    p.xz *= rot(.3*tt);
   
//    p.x -= 3.;
  //  p.xy = abs(p.xy)- .3;
    

     
    vec3 bp0 = p;

    p = fold(p);

    vec3 bp = p;
    float k;
    
    p = bp0;
    p.x = sabsk(p.x, 1.2) - 2.;
    p.xy *= rot(p.z*.3+tt);

    vec4 p4 = inverseStereographic(p,k);

    p4.y -= SIN(.3*tt);
    p4.zy *= rot(tt*.5);
    p4.xw *= rot(tt*.5);

    
    float d = fTorus(p4);
    d = abs(d);
    d -= .2;

    d = fixDistance(d, k);
  
    p = bp;
    
    //p = fold(p);
    
    p.xy *= rot(1.*p.z*.5+tt);
    p.xz *= rot(.5*p.y+.5*tt);
        
    p.xy -= sin(p.z*mix(2., 15., SIN(.7*tt))+tt)*.2;
    bp = p;

    p.x -= .3;
    p.xz = sabsk(p.xz, .4) - .5*SIN(tt);
    p.xz*= rot(tt);

    p.xz -= vec2(sin(.4*tt)*.3, cos(.3*tt)*.4);
    float d1 = length(p) - 1.4;
    
   // d1 = abs(d1)-.1;
    p = bp;
    
    p.x += .8;
    p.xy = sabsk(p.xy, .4) + 1.*SIN(.5*tt);
    p.xz += vec2(sin(tt)*.4, cos(tt)*1.5);
          
    float d2 = smax(d1, -(length(p) - 1.4), 1.);

    d = smax(d, (d2)-.5, .5);
    
  
    
    return d*.8;

}


vec3 pal(float x) {
    return .5 + .5*cos( 6.28318*(vec3(1.0,1.0,1.0)*x+vec3(0.0,0.3,0.5)));
}


vec3 getRayDir(vec2 uv, vec3 p, vec3 l, float z) {
    
    // camera system
    vec3 f = normalize(l - p),  // forward vector
         r = normalize(cross(vec3(0, 1, 0), f)), // right vector
         u = cross(f, r), // up vector
         c = p + f * z, // center of virtual screen
         i = c + uv.x * r + uv.y * u, // intersection with screen
         rd = normalize(i - p);  // ray direction
         
    return rd;
    
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
   	vec2 uv = (fragCoord - .5*iResolution.xy)/iResolution.y;

    tt = iTime + 30.;

    //uv = uv.yx;
    vec3 col;
    
    ro = vec3(.0, 0, -4);
    vec3 lookat = vec3(0, 0, 0), p;

    vec3 rd = getRayDir(uv, ro, lookat, 1.);
       
    
    float mat = 0.,
          t   = 0.,
          d   = 0.;
 
    float alpha = 0.;
    
    p = ro;
    float MAX_DIST = 15.;
    vec3 c;
    for (float i = 0.; i < 100.; i++) {
        t += max(0.01, abs(d));
        p = ro + rd * t;
        d = map(p);

        c = vec3(max(0., .01 - abs(d)));

        c += vec3(0.271,0.094,0.306) * 0.002;
        c *= smoothstep(20., .4, length(t));
        float l = smoothstep(MAX_DIST, .1, t);
        c *= l;
        
        c *= pal(l * 8. - .9);

        col += c;

        if (t > MAX_DIST) {
            break;
        }
    }

    // Tonemapping and gamma
    //col = pow(col, vec3(1. / 1.8)) * 2.;
    
   // col = pow(col, vec3(2.)) * 3.;
    col *= 8.;
    col *= tanh(col);

    col = pow(col, vec3(1. / 2.2));
    
    fragColor = vec4(col, alpha);
}