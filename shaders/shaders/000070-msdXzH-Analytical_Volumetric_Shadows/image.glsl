// Image (image) — Analytical Volumetric Shadows by me_123
// https://www.shadertoy.com/view/msdXzH

// Created by me_123. March 2023

#define COUNT 10

#define MAX 1000.
vec3 getRay(in vec3 cameraDir, in vec2 uv) {
    vec3 cameraPlaneU = vec3(normalize(vec2(cameraDir.y, -cameraDir.x)), 0);
    vec3 cameraPlaneV = cross(cameraPlaneU, cameraDir) ;
	return normalize(cameraDir + uv.x * cameraPlaneU + uv.y * cameraPlaneV);
}
vec3 hash33(uvec3 q)
{// by David Hoskins.
	q *= uvec3(1597334673U, 3812015801U, 2798796415U);
	q = (q.x ^ q.y ^ q.z)*uvec3(1597334673U, 3812015801U, 2798796415U);
	return vec3(q) * (1.0 / float(0xffffffffU));
}
vec3 hash31(uint q)
{// by David Hoskins.
	uvec3 n = q * uvec3(1597334673U, 3812015801U, 2798796415U);
	n = (n.x ^ n.y ^ n.z) * uvec3(1597334673U, 3812015801U, 2798796415U);
	return vec3(n) * (1.0 / float(0xffffffffU));
}
float hash11(uint q)
{// by David Hoskins.
	uvec2 n = q * uvec2(1597334673U, 3812015801U);
	q = (n.x ^ n.y) * 1597334673U;
	return float(q) * (1.0 / float(0xffffffffU));
}
vec3 noise(in float x) {
    return mix(hash31(uint(floor(x))),hash31(1u+uint(floor(x))),0.5*(1.-cos(3.14159*fract(x))));
}
vec3 fbm(in float x) {
    vec3 v = vec3(0);
    for (int i = 0; i < 5; i += 1) {
        v += noise(x*float(1<<i))*pow(2., -float(i));
    }
    return v*0.5 - 0.5;
}
vec2 sphere( in vec3 ro, in vec3 rd, float r )
{
    float b = dot( ro, rd );
    vec3 qc = ro - b*rd;
    float k = r*r - dot( qc, qc );
    if(k < 0.0) return vec2(MAX);
    k = sqrt( k );
    return vec2(-b-k, -b+k);
}
float fog(in vec3 o, in vec3 d, in vec2 x) {
    // integral of 1/((o.x+d.x*x)^2 + (o.y+d.y*x)^2 + (o.y+d.y*x)^2) dx
    // from x.x to x.y
    float dd = dot(d, d),
    d0 = dot(o, d),
    q = 1./(sqrt(0.00005+dd*(o.z*o.z-d0*d0/dd+dot(o.xy, o.xy))));
    vec2 k = atan((dd*x+d0)*q);
    return (k.y-k.x)*q*0.05;
}
const float dark = 0.2; //dark areas
vec3 findShadow(in vec3 o, in vec3 d, in vec3 p, in vec3 light) {
    //sphere shadow intersection from a ray and light position
    // it just points the space to the sphere with a look at matrix
    // then intersects a ray with x^2+y^2-z^2=0
    o -= light;
    p -= light;
    float see = length(light)-1./length(light);
    vec3 z = normalize(light);
    vec3 x = normalize(cross(vec3(0, 0, 1), z));
    vec3 y = normalize(cross(z, x));
    mat4 mat = mat4(vec4(x,0),vec4(y,0),vec4(z,0),vec4(light,1));
    o = -(vec4(o, 0)*mat).xyz;
    p = -(vec4(p, 0)*mat).xyz;
    d = -(vec4(d, 0)*mat).xyz;
    float v = 1./(dot(light, light)-1.0);
    float j = ((p.x*p.x+p.y*p.y-p.z*p.z*v) > 0.0 || p.z < see) ? 1.0 : -1.0;
    if (dot(light, light) < 1.0) j = -1.;
    float a = d.x*d.x+d.y*d.y-(d.z*d.z)*v;
    float b = 2.*(o.x*d.x + o.y*d.y - v*o.z*d.z);
    float c = o.x*o.x + o.y*o.y - o.z*o.z*v;
    vec2 h = (-b + vec2(1, -1)*sqrt((b*b - 4.*a*c)))/(2.*a);
    if (h.x < 0.0) h.x = MAX;
    if (o.x*o.x+o.y*o.y-o.z*o.z*v < 0.0 && o.z > 0.0) return vec3(0, h.x, j);
    if (o.z+d.z*h.y < see) return vec3(MAX, MAX, j);
    if (isnan(h.x) || h.y < 0.0) return vec3(vec2(MAX), j);
    if (h.x-h.y < 0.0) h.x = MAX;
    return vec3(h.yx, j);
}
float time = 0.0;
vec3 getColor(in vec3 ro, in vec3 rd) {
    vec3 normal = vec3(0);
    float dist = 0.0;
    vec3 albedo = vec3(0);
    int mat = -1;
    dist = sphere(ro, rd, 1.0).x;
    if (dist < MAX) {
        normal = ro+rd*dist;
        mat = 0;    
    }
    float ground = -(ro.z+1.)/rd.z;
    if (ground < 0.0) ground = MAX;
    if (ground < dist) {
        normal.z = float(mat = 1);
        dist = ground;
    }
    vec3 p = ro+rd*dist;
    albedo = vec3(mat==0?1.:(int(floor(p.x)+floor(p.y))%2==0?0.5:1.));
    albedo *= 2.-1./pow(0.7+length((ro+rd*dist)-vec3(0,0,-1)),2.0);
    dist = min(dist, ground);
    vec3 volumetric = vec3(0);
    vec3 light = vec3(0);
    for (int i = 0; i < COUNT; i += 1) {
        vec3 lightPos = sin(hash31(uint(i+10))*10.+hash31(uint(i+100))*time*0.5)*2.5;
        vec3 lightColor = 0.005/pow(hash31(uint(i+10)), vec3(1.25));
        if (i == 1) lightColor *= 10.;
        vec3 god = findShadow(ro, rd, p, lightPos);
        volumetric += lightColor*fog(ro-lightPos, rd, vec2(0, dist));
        light += lightColor*((god.z > 0.0 || mat == 0)?1.0:dark)*clamp(mix(dot(normal, normalize(lightPos-p)), 1.0, 0.5), dark, 1.0)/(dot(lightPos-p, lightPos-p));
        if (god.x != MAX) volumetric -= lightColor*fog(ro-lightPos, rd, vec2(min(god.x, dist), min(god.y, dist)));
    }
    volumetric += albedo*light;
    return (2.-1./(volumetric*(3.0+0.5*sin(time))+0.5))*0.5;
}
float Falloff = 0.25;
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    time = 0.3*((sin(iTime)+1.0)*5.+time);
    vec2 uv = (fragCoord.xy-iResolution.xy*0.5)/iResolution.y;
    vec2 m = vec2(iTime*0.8, sin(iTime*0.25)*0.6+3.1415*0.5);
    if (iMouse.z > 0.0) m = (iMouse.xy/iResolution.xy)*vec2(6.28, 3.14159263);
    vec3 ro = vec3(sin(m.y) * cos(-m.x), sin(m.y) * sin(-m.x), max(cos(m.y), -0.1))*5.0;
    vec3 rd = getRay(-normalize(ro), uv);
    ro += fbm(iTime)*0.5;
    vec3 color = getColor(ro, rd)+hash33(uvec3(fragCoord.xy, iTime))*0.00390625;
    fragColor = vec4(color, 1);
}