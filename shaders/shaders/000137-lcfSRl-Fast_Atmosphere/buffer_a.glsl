// Buffer A (buffer) — Fast Atmosphere by Fewes
// https://www.shadertoy.com/view/lcfSRl

// Buffer A renders the terrain during the first frame, or whenever the resolution changes.

#define MAX_SAMPLES 512
#define MAX_DIST 200000.0

float Terrain(vec2 p)
{
    p /= TERRAIN_SCALE;
    p += TERRAIN_OFFSET;
    
    float a = 0.0;
    float b = 1.0;
    vec2 d = vec2(0.0);
    vec3 n = vec3(0.0);
    
    float na = 0.5;
    
    vec2 pf = p;
    for (int i = 0; i < TERRAIN_OCTAVES; i++)
    {
        vec3 nl = Noised(pf);
        //nl.x = 1.0 - abs(nl.x - 0.5) * 2.0;
        n += nl * na / (1.0+dot(d,d));
        d += nl.yz;       
        na *= TERRAIN_GAIN;
        pf = m*pf*TERRAIN_LACUNARITY;
        pf += vec2(-d.y,d.x) * 0.1; // curl warp
    }

    return (n.x - TERRAIN_CENTER) * TERRAIN_HEIGHT;
}
vec3 TerrainNormal(vec3 p)
{
    float d = 1.0;
    vec3 p0 = p;
    vec3 p1 = p + vec3(d, 0, 0);
    vec3 p2 = p + vec3(0, 0, d);
    p0.y = Terrain(p0.xz);
    p1.y = Terrain(p1.xz);
    p2.y = Terrain(p2.xz);
    
    return normalize(cross(p2 - p0, p1 - p0));
}
float MarchTerrain(vec3 ro, vec3 rd, float maxDist, out vec3 normal)
{
    float t = 0.0;
    float ds = 0.0;
    for (int i = 0; i < MAX_SAMPLES; i++)
    {
        vec3 p = ro + rd * t;
        float h = p.y - Terrain(p.xz);
        if (h < 0.0)
        {
            t -= ds * 0.5;
            break;
        }
        ds = t * 1e-3 + h * 0.7;
        t += ds;
        if (t > maxDist)
        {
            t = -1.0;
            break;
        }
    }
    normal = TerrainNormal(ro + rd * t);
    return t;
}

vec4 Render(vec2 pixel)
{
    vec2 uv = pixel / iResolution.xy;
    
    vec3 ro, rd;
    GetCamera(uv, iResolution, ro, rd);
    
    vec3 normal;
    float t = MarchTerrain(ro, rd, MAX_DIST, normal);
    
    vec2 t_planet = PlanetIntersection(ro, rd);
    if (t_planet.x > 0.0 && t < 0.0)
    {
        t = t_planet.x;
    }
    
    vec4 color = vec4(t, normal.xz, 0.0);
    
    if (t > 0.0)
    {
        vec3 p = ro + rd * t;
        float d = 100.0;
        vec3 n1 = TerrainNormal(p - vec3(d, 0, 0));
        vec3 n2 = TerrainNormal(p + vec3(0, 0, d));
        vec3 n3 = TerrainNormal(p - vec3(d, 0, 0));
        vec3 n4 = TerrainNormal(p + vec3(0, 0, d));
        color.w = (dot(n1, n2) + dot(n3, n4)) / 2.0;
    }
    
    if (pixel.x < 1.0 && pixel.y < 1.0)
    {
        color.w = iResolution.x;
    }
    
    return color;
}

void mainImage(out vec4 fragColor, in vec2 fragCoord )
{   
    vec2 uv = fragCoord / iResolution.xy;
    
    vec4 prev = texture(iChannel2, uv);
    vec4 prevData = texture(iChannel2, vec2(0.0));
    
    if (iFrame > 0 && iResolution.x == prevData.w)
    {
        // Terrain already rendered, exit
        fragColor = prev;
#ifdef CACHE_TERRAIN
        return;
#endif
    }
    
    fragColor = Render(fragCoord);
}