// Buffer C (buffer) — Fast Atmosphere by Fewes
// https://www.shadertoy.com/view/lcfSRl

// Buffer C renders clouds (final lighting is composited in Image)

float Noise(vec2 x)
{
    return textureLod(iChannel0, x * 0.002, 0.0).x;   
}

// https://www.shadertoy.com/view/csSfRK
#define PARAMS_LINEAR_RAMP  vec2(0.00, 0.00)
#define PARAMS_CUMULUS      vec2(0.4, 0.6)
#define PARAMS_CUMULONIMBUS vec2(0.70, 0.98)
float CloudShape(float x, float y, vec2 shapeParams)
{  
    shapeParams.x *= shapeParams.y;
    shapeParams.y = 1.0 / (1.0 - shapeParams.y);
    float anvil = 1.0 - sq(abs(y - 0.5) * 2.0);
	return saturate(x - anvil * shapeParams.x - pow(y, shapeParams.y));
}

#define SCALE 1000.0
#define OFFSET vec2(4.0, 8.0)
#define SHAPE PARAMS_CUMULUS

float remap01(float x, float a, float b) { return clamp((x - a) / (b - a), 0.0, 1.0); }

float Height(vec3 p)
{
    return length(p - PLANET_CENTER) - PLANET_RADIUS;
}

float Cloud(vec3 p)
{
    vec2 wind = vec2(0, iTime);
    float y = (Height(p) - CLOUD_PLANE_BOT) / (CLOUD_PLANE_TOP - CLOUD_PLANE_BOT);
    const float scale = 1.0 / SCALE;
    float n = Noise(p.xz * scale + OFFSET + vec2(-0.2, 0.3) * iTime);
    float d = CloudShape(n - 1.0 + CLOUD_COVERAGE * 2.0, y, SHAPE);
    
    float n2 = Noise(p.xz * scale * 8.0 - vec2(0.2, 0.0) * iTime);
    float n3 = Noise(p.xz * scale * 40.0 + vec2(1.0, 0.0) * iTime);
    
    d = remap01(d, (1.0 - n2) * 0.3, 1.0);
    d = remap01(d, (1.0 - n3) * 0.1, 1.0);
    
    d *= smoothstep(0.0, 0.75, y);
    
    return sq(d) * CLOUD_DENSITY;
}

vec3 Lum(vec3 p, vec3 ld, float costh, float ext, float dither)
{
    float le = 0.0;
    float ae = 0.0;
    int sc = 4;
    float ss = (CLOUD_PLANE_TOP - CLOUD_PLANE_BOT) / float(sc);
    for (int j = 0; j < sc; j++)
    {
        vec3 lp = p + ld * (float(j) + dither) * ss;
        le += Cloud(lp) * ss;
    }
    sc = 2;
    for (int j = 0; j < sc; j++)
    {
        vec3 lp = p + vec3(0, 1, 0) * (float(j) + dither) * ss;
        ae += Cloud(lp) * ss;
    }
    float single = exp(-le) * PhaseM(costh, 0.85);
    float multi = exp(-le * 0.05) * PhaseR(costh);
    multi *= 1.0 - exp(-ext * 6e2);
    return vec3(single + multi, (exp(-ae) + exp(-ae * 0.05)) * 0.5, 0);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
#ifndef ENABLE_CLOUDS
    fragColor = vec4(0, 0, 0, 0);
    return;
#endif

    vec2 uv = fragCoord / iResolution.xy;
    
    vec3 dither = textureLod(iChannel1, fragCoord / vec2(1024), 0.0).xyz;
    dither = fract(dither + (0.61803398875 * float(iFrame & 255)));
    
    vec4 mouse = iMouse / iResolution.xyxy;
    float time = -iTime * 0.2 + 3.5;
    vec2 sunPos = 0.5 + vec2(cos(time) * 0.4, sin(time) * 0.45);
    if (iMouse.x > 10.0)
    {
        sunPos = mouse.xy;
    }
    vec3 sunDir;
    vec3 foo;
    GetCamera(sunPos, iResolution, foo, sunDir);

    vec3 ro, rd;
    GetCamera(uv, iResolution, ro, rd);
    
    float costh = dot(rd, sunDir);
    
    vec2 t1 = SphereIntersection(ro, rd, PLANET_CENTER, PLANET_RADIUS + CLOUD_PLANE_BOT);
    vec2 t2 = SphereIntersection(ro, rd, PLANET_CENTER, PLANET_RADIUS + CLOUD_PLANE_TOP);
    vec2 tp = PlanetIntersection(ro, rd);
    
    float enter = t1.y;
    float exit = t2.y;
    if (t1.x > 0.0)
    {
        exit = t1.x;
    }
    if (t2.x > 0.0)
    {
        enter = t2.x;
    }
    if (ro.y > CLOUD_PLANE_BOT && ro.y < CLOUD_PLANE_TOP)
    {
        enter = 0.0;
    }
    if (tp.x > 0.0)
    {
        enter = min(enter, tp.x);
        exit = min(exit, tp.x);
    }
    
    enter = max(0.0, enter);

#ifdef ENABLE_TERRAIN
    vec4 terrain = texture(iChannel3, uv);
    if (terrain.x > 0.0)
    {
        if (terrain.x < enter)
        {
            fragColor = vec4(0, 0, 0, INFINITY);
            return;
        }
        exit = min(exit, terrain.x);
    }
#endif
    
    vec2 shape = PARAMS_CUMULUS;
    float coverage = 0.9;
    
    float depth = 0.0;
    
    vec3 s = vec3(0.0);
    float tsm = 1.0;
    int sc = 64;
    float ss = 100.0;
    float t = enter + ss * 0.5;
    for (int i = 0; i < sc; i++)
    {
        vec3 p = ro + rd * (t + ss * dither.x);
        
        float h = Height(p);
        if (h < CLOUD_PLANE_BOT || h > CLOUD_PLANE_TOP)
        {
            break;
        }
        
        float le = Cloud(p);
        vec3 ll = vec3(0.0);
        if (le > 0.0)
        {
            ll = Lum(p, sunDir, costh, le, dither.y);
            vec4 at;
        }
        
        float lt = exp(-le * ss);
        float is = tsm * (1.0 - lt);

        depth = mix(depth, t, sq(tsm));
        
        s += ll * is;
		tsm *= lt;
        
        if (tsm < 1e-5)
        {
            break;
        }
        
        t += ss;
        ss *= 1.05;
        
        if (t > exit)
        {
            break;
        }
    }
    
    vec4 prev = texture(iChannel2, uv);
    
    float temporalStability = iMouse.z > 0.0 || iFrame < 1 ? 0.0 : TEMPORAL_ACCUMULATION_CLOUDS;
    // Doesn't work for some reason
    vec4 prevData = texture(iChannel2, vec2(0.0));
    float screenHash = iResolution.x + iResolution.y;
    float screenHashPrev = prevData.z;
    float sunHash = sunPos.x + sunPos.y;
    float sunHashPrev = prevData.w;
    // Doesn't work for some reason
    //temporalStability = GetTemporalStability(iFrame, sunHash, sunHashPrev, TEMPORAL_ACCUMULATION_CLOUDS, 50.0);
    
    // vec4(direct, ambient, alpha, weighted depth)
    fragColor = max(vec4(0.0), mix(vec4(s.xy / (1.0 - tsm), 1.0 - tsm, depth), prev, temporalStability));
    
    if (fragCoord.x < 1.0 && fragCoord.y < 1.0)
    {
        // Store some data in corner pixel
        fragColor.z = screenHash;
        fragColor.w = sunHash;
    }
}